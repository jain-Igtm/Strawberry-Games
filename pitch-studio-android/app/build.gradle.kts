import java.util.Base64

plugins {
    id("com.android.application")
}

val stableDebugStore = layout.buildDirectory.file("signing/pitchstudio-debug.p12").get().asFile
stableDebugStore.parentFile.mkdirs()
stableDebugStore.writeBytes(Base64.getMimeDecoder().decode(
    rootProject.file("pitchstudio-debug.p12.b64").readText()
))

android {
    namespace = "com.strawberry.pitchstudio"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.strawberry.pitchstudio"
        minSdk = 26
        targetSdk = 35
        versionCode = 4
        versionName = "0.3.1"

        ndk {
            abiFilters += listOf("arm64-v8a", "armeabi-v7a")
        }

        externalNativeBuild {
            cmake {
                cppFlags += listOf("-std=c++17")
            }
        }
    }

    ndkVersion = "27.0.12077973"

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    signingConfigs {
        create("stableDebug") {
            storeFile = stableDebugStore
            storePassword = "strawberry-debug"
            keyAlias = "pitchstudio"
            keyPassword = "strawberry-debug"
            storeType = "PKCS12"
        }
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.getByName("stableDebug")
        }
        release {
            isMinifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {
    testImplementation("junit:junit:4.13.2")
}
